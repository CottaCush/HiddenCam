ext.modulePackageId = ""

if (!project.rootProject.file('local.properties').exists()) {
  println "Not applying bintrayconfig.gradle"
  return
}
apply plugin: 'com.novoda.bintray-release'

def getBintrayUserAndKey() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return [
      properties.getProperty("bintray.user"),
      properties.getProperty("bintray.apikey")
  ]
}

if (versions == null || versions.publishVersion == null) {
  throw new IllegalStateException("Unable to reference publishVersion")
}

task checkBintrayConfig {
  doLast {
    def (user, key) = getBintrayUserAndKey()
    if (user == null || user.isEmpty() ||
        key == null || key.isEmpty()) {
      throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
    }
  }
}

afterEvaluate {
  bintrayUpload.dependsOn checkBintrayConfig
}

def (user, key) = getBintrayUserAndKey()
publish {
  bintrayUser = user
  bintrayKey = key
  userOrg = 'cottacush'
  groupId = 'com.cottacush'
  artifactId = 'HiddenCamera'
  publishVersion = versions.publishVersion
  desc = 'A library that allows you to capture images secretly, in background without alerting users.'
  website = 'https://github.com/CottaCush/HiddenCam'
  dryRun = false
}

ext.modulePackageId = publish.groupId + '.' + publish.artifactId